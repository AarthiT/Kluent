buildscript {
    ext.kotlin_version = '1.1.+'
    ext.spek_version = "1.0.+"
    ext.mockito_version = "1.3.+"
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

group 'org.amshove.kluent'
version '1.16'

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
    maven {
        url = 'http://repository.jetbrains.com/all'
    }
}

dependencies {
    compile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "com.nhaarman:mockito-kotlin:$mockito_version"
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.spek:spek:$spek_version"
    testCompile "com.nhaarman:mockito-kotlin:$mockito_version"
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintrayUpload.dependsOn install

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['PublishKluent']
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "Kluent"
        licenses = ['MIT']
        vcsUrl = 'https://github.com/MarkusAmshove/Kluent'
    }
}
publishing {
    publications {
        PublishKluent(MavenPublication) {
            from components.java
            groupId 'org.amshove.kluent'
            artifactId 'kluent'
            version project.version
        }
    }
}
