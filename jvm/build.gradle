apply plugin: 'kotlin-platform-jvm'

def isAndroid = project.rootProject.hasProperty("ANDROID")
if(isAndroid) {
    logger.warn("Android build!")
}

// TODO: rename 'kluent' to 'kluent-jvm' for 2.0
def artifactName = isAndroid ? 'kluent-android' : 'kluent'
configurePublishing(artifactName)

def mockito_kotlin_version = "2.1.0"

dependencies {
    expectedBy project(':kluent-common')
    implementation libs.kotlin_stdlib_jvm
    implementation libs.kotlin_reflect
    implementation libs.kotlin_test_jvm
    if(isAndroid) {
        runtime libs.kotlin_stdlib_jvm
        runtime libs.kotlin_reflect
        runtime libs.kotlin_test_jvm
    }

    testImplementation libs.junit
    testImplementation libs.kotlin_test_jvm
    testImplementation libs.kotlin_test_junit


    // TODO: To be removed
    implementation libs.junit
    implementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"
}

sourceSets {
    main {
        kotlin {
            srcDir 'src/main/kotlin'

            if(isAndroid) {
                exclude '**/*Backtick*'
            }
        }
    }
    test {
        kotlin {
            srcDir 'src/test/kotlin'

            if(isAndroid) {
                exclude '**/backtick*/**', '**/*Backtick*'
            }
        }
    }
}

tasks.withType(compileKotlin.getClass()) {
    kotlinOptions {
        jvmTarget = isAndroid ? '1.6' : '1.8'

        freeCompilerArgs += [
                '-Xuse-experimental=kotlin.Experimental'
        ]
    }
}
